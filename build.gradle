import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "java-library"
    id "com.github.johnrengelman.shadow" version "8.1.0"
    id "maven-publish"
}

group = 'co.mcGalaxy'
version = '1.0-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(21))
    disableAutoTargetJvm()
}

compileJava {
    options.compilerArgs += ['-parameters']
    options.fork = true
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    mavenLocal()

    maven {
        url = "https://repo.rosewooddev.io/repository/public/" }
    maven {
        url = "https://repo.papermc.io/repository/maven-public/" }
    maven {
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven {
        url = 'https://repo.extendedclip.com/releases/'
    }
}

dependencies {
    api "dev.rosewood:rosegarden:${gardenVersion}"

    // Decide whether you want to use Spigot or Paper API (Paper is recommended)
    compileOnly "io.papermc.paper:paper-api:${spigotVersion}-R0.1-SNAPSHOT"
//    compileOnly "org.spigotmc:spigot-api:${spigotVersion}-R0.1-SNAPSHOT"
    api 'dev.rosewood:rosegarden:1.4.5-Menu-0.2-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.11.6'
}

shadowJar {
    archiveClassifier.set(null)

    relocate("dev.rosewood.rosegarden", "${project.group}.myplugin.rosegarden")
    destinationDirectory = "C:\\Users\\mspic\\Desktop\\Build Tools\\plugins" as File
    minimize()

    // If you're not using SQL, you can exclude the following packages
    exclude 'dev/rosewood/rosegarden/lib/hikaricp/**/*.class'
    exclude 'dev/rosewood/rosegarden/lib/slf4j/**/*.class'

}

// Include version replacement
processResources {
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        filter ReplaceTokens, tokens: ["version": project.property("version")]
        duplicatesStrategy DuplicatesStrategy.INCLUDE
    }
}

// Jar Publishing
publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
            artifactId = "GalaxyTitles"
            pom {
                name = "GalaxyTitles"
            }
        }
    }

    repositories {
        if (project.hasProperty('mavenUser') && project.hasProperty('mavenPassword')) {
            maven {
                credentials {
                    username project.mavenUser
                    password project.mavenPassword
                }

                def releasesRepoUrl = 'https://repo.rosewooddev.io/repository/public-releases/'
                def snapshotsRepoUrl = 'https://repo.rosewooddev.io/repository/public-snapshots/'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }
}

build.dependsOn shadowJar